buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.guardsquare:proguard-gradle:7.6.0'
    }
}

plugins {
    id 'java'
    id 'application'
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(8))
    }

    sourceCompatibility = JavaVersion.VERSION_1_6
    targetCompatibility = JavaVersion.VERSION_1_6
}

application {
    mainClass = "jagex3.client.Client"
}

jar {
    manifest {
        attributes(
            'Main-Class': 'jagex3.client.Client'
        )
    }

    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

tasks.register('proguard', proguard.gradle.ProGuardTask) {
    configuration file('proguard.pro')

    injars(tasks.named('jar', Jar).flatMap { it.archiveFile })

    outjars(layout.buildDirectory.file("libs/${project.name}.rel.jar"))
}

// Task to load and summarize deobfuscation mappings
tasks.register('loadMappings', JavaExec) {
    group = 'deob'
    description = 'Load and summarize deobfuscation mappings from proguard.map'
    classpath = sourceSets.main.runtimeClasspath
    main = 'deob.MappingLoader'
    args = ['proguard.map']
}

// Task to apply mappings and rename source files
tasks.register('applyMappings', JavaExec) {
    group = 'deob'
    description = 'Apply deobfuscation mappings to Java source files'
    classpath = sourceSets.main.runtimeClasspath
    main = 'deob.MappingApplier'
    args = ['proguard.map', 'src/main/java']
}
